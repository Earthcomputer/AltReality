buildscript {
	repositories {
		mavenCentral()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven { 
			name = 'Jitpack'
			url 'https://jitpack.io/'
		}
		maven {
			name = 'HalfOf2'
			url = 'https://storage.googleapis.com/devan-maven/'
		}
	}
	dependencies {
		classpath 'com.github.Chocohead:Fabric-Loom:d823377'
	}
}

apply plugin: 'fabric-loom'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { 
		name = 'Jitpack'
		url 'https://jitpack.io/'
	}
	maven {
		name = 'HalfOf2'
		url = 'https://storage.googleapis.com/devan-maven/'
	}
}

minecraft {
	//Skip old versions of libraries which Mojang use but we don't need
	addLibraryFilter {library ->
		return !(library.startsWith('net.minecraft:launchwrapper:') ||
					library.startsWith('net.sf.jopt-simple:jopt-simple:') ||
					library.startsWith('org.ow2.asm:asm-all:'))
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings "io.github.minecraft-cursed-legacy:plasma:b1.7.3-build.${project.plasma_build}"

	modImplementation("io.github.minecraft-cursed-legacy:cursed-fabric-loader:${project.loader_version}") {
		transitive false
	}

	implementation("com.mojang:authlib:${project.authlib_version}") {
		transitive = false
	}
	include("com.mojang:authlib:${project.authlib_version}") {
		transitive = false
	}
	implementation "org.apache.logging.log4j:log4j-api:${project.log4j_version}"
	include "org.apache.logging.log4j:log4j-api:${project.log4j_version}"
	implementation "org.apache.logging.log4j:log4j-core:${project.log4j_version}"
	include "org.apache.logging.log4j:log4j-core:${project.log4j_version}"
	implementation "org.apache.commons:commons-lang3:${project.commons_lang3_version}"
	include "org.apache.commons:commons-lang3:${project.commons_lang3_version}"
	implementation "commons-io:commons-io:${project.commons_io_version}"
	include "commons-io:commons-io:${project.commons_io_version}"
	implementation "commons-codec:commons-codec:${project.commons_codec_version}"
	include "commons-codec:commons-codec:${project.commons_codec_version}"
}

processResources {
	inputs.property 'version', project.version

	from(sourceSets.main.resources.srcDirs) {
		include 'fabric.mod.json'
		expand 'version': project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'fabric.mod.json'
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
